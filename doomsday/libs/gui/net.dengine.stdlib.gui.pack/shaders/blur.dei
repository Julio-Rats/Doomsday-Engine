fx {

# Blurring is done in two passes: horizontal and vertical. This is more
# efficient than doing the equivalent amount of blurring in a single pass.
blur {
    # Both passes use the same vertex shader.
    common = "
        uniform mat4 uMvpMatrix;
        uniform vec4 uColor;
        uniform vec4 uWindow;

        in vec4 aVertex;
        in vec2 aUV;
        in vec4 aColor;

        out vec2 vUV;
        out vec4 vColor;

        void main(void) {
            gl_Position = uMvpMatrix * aVertex;
            vUV = uWindow.xy + aUV * uWindow.zw;
            vColor = aColor * uColor;
        }"

    shader horizontal {
        vertex $= fx.blur.common
        fragment = "
            uniform sampler2D uTex;
            uniform vec2 uBlurStep;

            in vec2 vUV;
            in vec4 vColor;

            void main(void) {
                vec4 sum = vec4(0.0);
                sum += texture(uTex, vec2(vUV.s - 4.0 * uBlurStep.s, vUV.t)) * 0.05;
                sum += texture(uTex, vec2(vUV.s - 3.0 * uBlurStep.s, vUV.t)) * 0.09;
                sum += texture(uTex, vec2(vUV.s - 2.0 * uBlurStep.s, vUV.t)) * 0.123;
                sum += texture(uTex, vec2(vUV.s - uBlurStep.s,       vUV.t)) * 0.154;
                sum += texture(uTex, vUV)                                    * 0.165;
                sum += texture(uTex, vec2(vUV.s + uBlurStep.s,       vUV.t)) * 0.154;
                sum += texture(uTex, vec2(vUV.s + 2.0 * uBlurStep.s, vUV.t)) * 0.123;
                sum += texture(uTex, vec2(vUV.s + 3.0 * uBlurStep.s, vUV.t)) * 0.09;
                sum += texture(uTex, vec2(vUV.s + 4.0 * uBlurStep.s, vUV.t)) * 0.05;
                out_FragColor = sum;
                out_FragColor.a = 1.0;
            }"
    }

    shader vertical {
        vertex $= fx.blur.common
        fragment = "
            uniform sampler2D uTex;
            uniform vec2 uBlurStep;

            in vec2 vUV;
            in vec4 vColor;

            void main(void) {
                vec4 sum = vec4(0.0);
                sum += texture(uTex, vec2(vUV.s, vUV.t - 4.0 * uBlurStep.t)) * 0.05;
                sum += texture(uTex, vec2(vUV.s, vUV.t - 3.0 * uBlurStep.t)) * 0.09;
                sum += texture(uTex, vec2(vUV.s, vUV.t - 2.0 * uBlurStep.t)) * 0.123;
                sum += texture(uTex, vec2(vUV.s, vUV.t - uBlurStep.t      )) * 0.154;
                sum += texture(uTex, vUV)                                    * 0.165;
                sum += texture(uTex, vec2(vUV.s, vUV.t + uBlurStep.t      )) * 0.154;
                sum += texture(uTex, vec2(vUV.s, vUV.t + 2.0 * uBlurStep.t)) * 0.123;
                sum += texture(uTex, vec2(vUV.s, vUV.t + 3.0 * uBlurStep.t)) * 0.09;
                sum += texture(uTex, vec2(vUV.s, vUV.t + 4.0 * uBlurStep.t)) * 0.05;
                out_FragColor = sum * vColor;
            }"
    }
}

} # fx
